// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load==1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the 
 * Screen and Keyboard chip specifications given in the book.
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    // Put your code here:
    // RAM16K in[16], address[14], load -> out[16]
    //   -> 0000 0000 0000 0000 - 0111 1111 1111 1111
    // Screen in[16], address[14], load -> out[16]
    //   -> 1000 0000 0000 0000 - 1011 1111 1111 1111
    // Keyboard -> out[16]
    //   -> 1100 0000 0000 0000 -

    // create flags
    Not(in=address[14], out=ramFlag);
    Not(in=address[13], out=tmpScreenFlag);
    And(a=address[14], b=tmpScreenFlag, out=screenFlag);

    // ram
    And(a=load, b=ramFlag, out=ramLoadFlag);
    RAM16K(in=in, address=address[0..13], load=ramLoadFlag, out=ramOut);

    // screen
    And(a=load, b=screenFlag, out=screenLoadFlag);
    Screen(in=in, address=address[0..12], load=screenLoadFlag, out=screenOut);

    // keyboard
    Keyboard(out=keyboardOut);

    Mux16(a=keyboardOut, b=screenOut, sel=screenFlag, out=out0);
    Mux16(a=out0, b=ramOut, sel=ramFlag, out=out);
}